plugins {
    id 'java'
    id 'antlr'
    id 'idea'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.4"
}

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.9.3"
    implementation "org.antlr:antlr4-runtime:4.9.3"
}

generateGrammarSource {
    maxHeapSize = "128m"
    arguments += ['-package', 'com.tiger.antlr', '-visitor', '-no-listener']
}
compileJava.dependsOn generateGrammarSource

sourceSets {
    main {
        java.srcDir 'generated-src/antlr/main/'
        java.srcDir 'src/main/java/'
    }
}

idea {
    module {
        settings {
            packagePrefix["generated-src/antlr/main/"] = "com.tiger.antlr"
            packagePrefix["src/main/java/"] = ""
        }
    }
}


compileJava.source sourceSets.main.java

clean{
    delete "generated-src"
}


test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// this is not strictly necessary but it is useful if you are not using an IDE
// it will create a JAR with all dependencies included


jar {
    manifest {
        attributes(
                'Main-Class': 'com.tiger.Main'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    baseName 'tigerc'
}
